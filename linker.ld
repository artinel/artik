OUTPUT_FORMAT(elf64-x86-64)
ENTRY(_start)


PHDRS {
	limine_requests PT_LOAD;
	text PT_LOAD;
	rodata PT_LOAD;
	data PT_LOAD;
	psffont PT_LOAD;
	bss PT_LOAD;
}

SECTIONS {
	. = 0xFFFFFFFF80000000;
	.limine_request : {
		KEEP(*(.limine_requests_start))
		KEEP(*(.limine_requests))
		KEEP(*(.limine_requests_end))
	} :limine_requests

	. = ALIGN(CONSTANT(MAXPAGESIZE));

	.text : {
		*(.text .text.*)

	} :text

	. = ALIGN(CONSTANT(MAXPAGESIZE));

	.rodata : {
		*(.rodata .rodata.*)
	} :rodata

	.note.gnu.build-id : {
		*(.note.gnu.build-id)
	} :rodata

	. = ALIGN(CONSTANT(MAXPAGESIZE));
	
	.data : {
		*(.data .data.*)
	} :data

	.bss : {
		*(.bss .bss.*)
		*(COMMON)

		/* Stack section - 16KB stack */
		. = ALIGN(16);
		_stack_bottom = .;
		. += 16K;
		_stack_top = .;
	} :data


	/DISCARD/ : {
		*(eh_frame*)
		*(.note .note.*)
	}
}
